/// Calculation table containing all key performance measures and metrics used throughout the report. This table has no data rows - it exists solely to organize and store DAX measures for trauma analysis including patient counts, mortality rates, timing metrics, and hospital-specific calculations.
table KeyMeasures
	lineageTag: 9b349d70-db67-490c-a8a7-959e6815a7ef

	/// Calculates the average time (in minutes) it took for patients to reach the Emergency Department from the time of injury. The calculation averages the TimeToED values across unique patient registration IDs to ensure each patient is counted once.
	measure 'Average TimeToED' =
			
			CALCULATE ( AVERAGE ( FACT_ATR[TimeToED] ), VALUES ( FACT_ATR[RegId] ) )
		formatString: 0.0
		lineageTag: 66797954-3738-4667-8c57-0b03bf7983c6

	/// Counts the total number of unique trauma cases by counting distinct patient registration IDs (RegId). This is the primary measure for counting patients in the trauma registry.
	measure 'Number of cases' =
			
			DISTINCTCOUNT ( FACT_ATR[RegId] )
		formatString: 0
		lineageTag: 728a6a72-bacb-4162-b3dd-81956398a241

	/// Counts the number of trauma cases that resulted in death. This filters the total number of cases to only include records where the discharge status indicates mortality (Deaths).
	measure Deaths =
			
			CALCULATE ( [Number of cases], DIM_DISCHAGRE[Mortality] = "Deaths" )
		lineageTag: 48816487-e768-47df-8eb9-cda0974dfa87

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Calculates the mortality rate as a percentage. The formula divides the number of deaths by the total number of cases. For example, if there are 10 deaths out of 100 cases, the result is 10%.
	measure 'Death %' =
			
			VAR survivors =
			    CALCULATE ( [Number of cases], DIM_DISCHAGRE[Mortality] = "Survivors" )
			VAR deaths =
			    CALCULATE ( [Number of cases], DIM_DISCHAGRE[Mortality] = "Deaths" )
			VAR result =
			    DIVIDE ( deaths, [Number of cases] )
			RETURN
			    result
		formatString: 0.0%;-0.0%;0.0%
		lineageTag: a405fd1c-c04f-4f2d-a6a7-a5ada48186e7

		changedProperty = FormatString

	/// Indicates whether a patient was transported directly to the hospital or was transferred from another facility. Returns 'Direct' if the patient came directly (OtherHospTransfer = 1 or -99), otherwise returns 'Transfer'.
	measure 'Direct Transport' =
			
			IF (
			    SELECTEDVALUE ( FACT_ATR[OtherHospTransfer] ) IN { 1, -99 },
			    "Direct",
			    "Transfer"
			)
		lineageTag: 3ea327f3-0e48-4bf0-92c8-e590a5f0bd12

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Calculates the percentage of cases for each discharge category. The formula divides cases in the selected discharge category by the total cases across all discharge types. This shows the distribution of discharge outcomes (e.g., home, rehabilitation, death).
	measure 'Discharge %' =
			
			VAR denominator =
			    CALCULATE (
			        [Number of cases],
			        REMOVEFILTERS ( DIM_DISCHAGRE[discharge_description] )
			    )
			VAR numerator = [Number of cases]
			VAR result =
			    DIVIDE ( numerator, denominator )
			RETURN
			    result
		formatString: 0.0
		lineageTag: e1660d34-aedb-4063-b7aa-04843986b2fe

	/// Returns the most recent date in the calendar that has at least one trauma case recorded. This helps identify the latest data available in the dataset.
	measure last_date =
			
			LASTNONBLANK ( 'CALENDAR'[cal_date], [Number of cases] )
		lineageTag: d5b6f0ac-f19a-42b0-bfd5-c03119d76c1d

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Calculates the average Length of Stay (LOS) in hospital, measured in days, using a robust outlier-removal method. The formula uses the Interquartile Range (IQR) to identify and exclude extreme outliers (values beyond 1.5 times the IQR from Q1/Q3), then calculates the average of the remaining values. This provides a more accurate average that isn't skewed by unusually long or short stays.
	measure LOS =
			
			VAR initial_table =
			    SUMMARIZE ( ALLSELECTED ( FACT_ATR ), FACT_ATR[RegId], FACT_ATR[LOS] )
			VAR calculation_table =
			    FILTER ( initial_table, NOT ( ISBLANK ( FACT_ATR[LOS] ) ) )
			VAR Q1 =
			    CALCULATE ( PERCENTILE.INC ( FACT_ATR[LOS], 0.25 ), calculation_table )
			VAR Q3 =
			    CALCULATE ( PERCENTILE.INC ( FACT_ATR[LOS], 0.75 ), calculation_table )
			VAR IQR = Q3 - Q1
			VAR lower_bound = ( Q1 - 1.5 * IQR )
			VAR upper_bound = ( Q3 + 1.5 * IQR )
			VAR final_table =
			    FILTER (
			        calculation_table,
			        FACT_ATR[LOS] > lower_bound && FACT_ATR[LOS] < upper_bound
			    )
			VAR result =
			    CALCULATE ( AVERAGE ( FACT_ATR[LOS] ), VALUES ( FACT_ATR[RegId] ), final_table )
			RETURN
			    result
		formatString: 0.0
		lineageTag: 67aa61d6-e744-4b0a-b1a1-26a43f6e4d17

	/// Returns the maximum (latest) date available in the Calendar table. This shows the end date of the date range covered by the data model.
	measure max_day =
			
			{ MAX ( 'CALENDAR'[cal_date] ) }
		lineageTag: 2c67b1c3-5982-4e08-972c-bb166eeb53ef

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Calculates the average time patients spent in the Emergency Department (in minutes) using outlier-removal logic. The formula uses the Interquartile Range (IQR) method to exclude extreme values that fall outside 1.5 times the IQR from the first and third quartiles (Q1 and Q3), then averages the remaining values for a more reliable measure.
	measure 'Time in ED' =
			
			VAR initial_table =
			    CALCULATETABLE (
			        ALLSELECTED ( FACT_ATR ),
			        NOT ( ISBLANK ( FACT_ATR[TimeInED] ) )
			    )
			VAR calculation_table =
			    SUMMARIZE ( initial_table, FACT_ATR[RegId], FACT_ATR[TimeInED] ) //FILTER(initial_table, NOT(ISBLANK(FACT_ATR[TimeInED])))
			VAR Q1 =
			    CALCULATE ( PERCENTILE.INC ( FACT_ATR[TimeInED], 0.25 ), calculation_table )
			VAR Q3 =
			    CALCULATE ( PERCENTILE.INC ( FACT_ATR[TimeInED], 0.75 ), calculation_table )
			VAR IQR = Q3 - Q1
			VAR lower_bound = ( Q1 - 1.5 * IQR )
			VAR upper_bound = ( Q3 + 1.5 * IQR )
			VAR final_table =
			    FILTER (
			        calculation_table,
			        FACT_ATR[TimeInED] > lower_bound && FACT_ATR[TimeInED] < upper_bound
			    )
			VAR result =
			    CALCULATE (
			        AVERAGE ( FACT_ATR[TimeInED] ),
			        VALUES ( FACT_ATR[RegId] ),
			        final_table
			    )
			RETURN
			    result
		formatString: 0.0
		lineageTag: 0a3a09f7-cb61-4980-bf8b-02ecf3aa3e3f

	/// Calculates the average time (in minutes) from injury to Emergency Department arrival, using robust outlier-removal. The formula identifies outliers using the IQR method (values beyond 1.5 times the IQR from Q1/Q3), excludes them, then calculates the average of the remaining values to avoid skewing from extreme cases.
	measure 'Time to ED' =
			
			VAR initial_table =
			    CALCULATETABLE (
			        ALLSELECTED ( FACT_ATR ),
			        NOT ( ISBLANK ( FACT_ATR[TimeToED] ) )
			    )
			VAR calculation_table =
			    SUMMARIZE (
			        initial_table,
			        FACT_ATR[RegId],
			        FACT_ATR[InstitutionID],
			        FACT_ATR[TimeToED]
			    )
			VAR Q1 =
			    CALCULATE ( PERCENTILE.INC ( FACT_ATR[TimeToED], 0.25 ), calculation_table )
			VAR Q3 =
			    CALCULATE ( PERCENTILE.INC ( FACT_ATR[TimeToED], 0.75 ), calculation_table )
			VAR IQR = Q3 - Q1
			VAR lower_bound = ( Q1 - 1.5 * IQR )
			VAR upper_bound = ( Q3 + 1.5 * IQR )
			VAR final_table =
			    FILTER (
			        calculation_table,
			        FACT_ATR[TimeToED] > lower_bound && FACT_ATR[TimeToED] < upper_bound
			    )
			VAR result =
			    CALCULATE (
			        AVERAGE ( FACT_ATR[TimeToED] ),
			        VALUES ( FACT_ATR[RegId] ),
			        final_table
			    )
			RETURN
			    result
		formatString: 0.0
		lineageTag: 892cca68-9253-4922-a2e0-6314aa5731a6

	/// Security measure that checks if the currently selected hospital is in the restricted list. Returns 1 if the hospital should be accessible to the current user, otherwise returns blank. This is used to control row-level security and determine which hospital data users can view.
	measure 'Restricted hospital id' =
			
			VAR hospital_id =
			    SELECTEDVALUE ( DIM_HOSPITAL[InstCodeId] )
			VAR hospital_role =
			    LOOKUPVALUE (
			        RESTRICTED_HOSPITAL[InstCodeId],
			        RESTRICTED_HOSPITAL[InstCodeId], hospital_id
			    )
			VAR result =
			    IF ( ISBLANK ( hospital_role ), BLANK (), 1 )
			RETURN
			    result
		lineageTag: a42df4a3-48b4-4fa6-892b-663cbd4a74d8

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Looks up and displays the hospital name based on the selected hospital ID from the restricted hospital list. If no match is found, it returns 'ALL'. This helps display user-friendly hospital names instead of codes.
	measure hospital_name =
			
			LOOKUPVALUE (
			    DIM_HOSPITAL[HospitalName],
			    DIM_HOSPITAL[InstCodeId], SELECTEDVALUE ( RESTRICTED_HOSPITAL[InstCodeId] ),
			    "ALL"
			)
		lineageTag: 8329a976-6149-4448-b1f0-6dd23cd73996

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Checks if the selected hospital from the restricted list matches a hospital in the main hospital dimension. Returns 'show' if there's a match, indicating the hospital exists in the current dataset. Used for conditional formatting or visibility controls.
	measure CurrentHospital =
			
			IF (
			    SELECTEDVALUE ( 'RESTRICTED_HOSPITAL'[InstCodeId] )
			        IN VALUES ( 'DIM_HOSPITAL'[InstCodeId] ),
			    "show"
			)
		lineageTag: 4f8e727a-1439-45c4-b109-779e4c02faaf

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Looks up the hospital name from the restricted hospital list based on the selected hospital ID from the hospital dimension. Returns 'ALL' if no match is found. This is useful for displaying hospital names in charts and visuals while respecting security restrictions.
	measure PlotHospital =
			
			LOOKUPVALUE (
			    'RESTRICTED_HOSPITAL'[HospitalName],
			    'RESTRICTED_HOSPITAL'[InstCodeId], SELECTEDVALUE ( 'DIM_HOSPITAL'[InstCodeId] ),
			    "ALL"
			)
		lineageTag: 3490f2b3-ca68-4e77-bf90-6f8bc77dc366

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Counts the total number of unique trauma cases in the national dataset by counting distinct patient registration IDs (RegId) from the FACT_NATIONAL table. This provides national-level injury statistics for comparison with regional data.
	measure 'National Injuries' =
			
			DISTINCTCOUNT ( FACT_NATIONAL[RegId] )
		lineageTag: 1318937b-0b78-4c0a-805d-aba6c31999bc

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	partition KeyMeasures-66b7a04d-45a5-4cef-a28d-2746228d3192 = m
		mode: import
		source =
				let
				    Source = Table.FromRows(Json.Document(Binary.Decompress(Binary.FromText("i45WSlSKjQUA", BinaryEncoding.Base64), Compression.Deflate)), let _t = ((type nullable text) meta [Serialized.Text = true]) in type table [Column1 = _t]),
				    #"Changed Type" = Table.TransformColumnTypes(Source,{{"Column1", type text}}),
				    #"Removed Columns" = Table.RemoveColumns(#"Changed Type",{"Column1"})
				in
				    #"Removed Columns"

	annotation PBI_ResultType = Table

